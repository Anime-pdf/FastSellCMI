plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'io.papermc.hangar-publish-plugin' version '0.1.3'
}

group = 'pdf.anime.fastsellcmi'
ext.projectVersion = '3.0.0'

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    jar.enabled = false
}

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-parameters"
}

repositories {
    mavenCentral() // configurate
    maven { url 'https://repo.papermc.io/repository/maven-public/' } // paper-api
    maven { url 'https://jitpack.io' } // vault-api
    maven { url 'https://repo.aikar.co/content/groups/aikar/' } // commands-api
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'

    // CMI
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly files('libs/CMIAPI-9.7.4.1.jar')

    // Config
    implementation 'org.spongepowered:configurate-core:4.2.0'
    implementation ('org.spongepowered:configurate-yaml:4.2.0') {
        exclude group: 'org.yaml', module: 'snakeyaml'
    }

    // Commands
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

build {
    dependsOn.add(shadowJar)
}

shadowJar {
    archiveClassifier = ''

    relocate 'org.spongepowered.configurate', 'pdf.anime.fastsellcmi.libs.configurate'
    relocate 'io.leangen.geantyref', 'pdf.anime.fastsellcmi.libs.geantyref'
    relocate 'net.kyori.option', 'pdf.anime.fastsellcmi.libs.kyori_option'
    relocate 'org.jspecify', 'pdf.anime.fastsellcmi.libs.jspecify'

    relocate 'co.aikar.commands', 'pdf.anime.fastsellcmi.libs.acf'
    relocate 'co.aikar.locales', 'pdf.anime.fastsellcmi.libs.locales'

    manifest {
        attributes["paperweight-mappings-namespace"] = "mojang"
    }
}

test {
    useJUnitPlatform()
}

hangarPublish {
    publications.register('plugin') {
        version = project.version as String
        id = System.getenv("HANGAR_ID")
        apiKey = System.getenv("HANGAR_API_TOKEN")
        channel = 'Release'

        platforms {
            paper {
                jar = shadowJar.archiveFile
                platformVersions = ['1.18.2-1.21.5']
                dependencies {
                    url('CMI', 'https://www.zrips.net/cmi/')
                    url('Vault', 'https://github.com/milkbowl/Vault')
                }
            }
        }
    }
}
