plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group 'pdf.anime.fastsellcmi'
ext.projectVersion = '2.1.0'

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    jar.enabled = false
}

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-parameters"
}

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://repo.panda-lang.org/releases' }
    maven { url 'https://repo.codemc.io/repository/maven-public/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'

    // CMI
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly files('libs/CMI-API9.6.5.0.jar')

    // Config
    implementation 'org.spongepowered:configurate-core:4.1.2'
    implementation ('org.spongepowered:configurate-yaml:4.1.2') {
        exclude group: 'org.yaml', module: 'snakeyaml'
    }

    // Commands
    implementation ('dev.rollczi:litecommands-bukkit:3.4.0') {
        exclude group: 'org.jetbrains', module: 'annotations'
    }

    // NBTAPI
    implementation 'de.tr7zw:item-nbt-api:2.12.2'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

build {
    dependsOn.add(shadowJar)
}

shadowJar {
    relocate 'de.tr7zw.changeme.nbtapi', 'pdf.anime.fastsellcmi.libs.nbtapi'
    relocate 'org.spongepowered.configurate', 'pdf.anime.fastsellcmi.libs.configurate'
    relocate 'io.leangen.geantyref', 'pdf.anime.fastsellcmi.libs.geantyref'
    relocate 'dev.rollczi.litecommands', 'pdf.anime.fastsellcmi.libs.litecommands'
    relocate 'panda.std', 'pdf.anime.fastsellcmi.libs.panda.std'
    incrementBuildVersion()
}

test {
    useJUnitPlatform()
}

def createPropertiesIfNotExist() {
    def versionPropsFile = file('version.properties')
    if(!versionPropsFile.exists()) {
        versionPropsFile.createNewFile()
        BufferedWriter writer = new BufferedWriter(new FileWriter('version.properties'))
        writer.write("build=0")
        writer.close()
    }
}

def getBuildVersion() {
    createPropertiesIfNotExist()
    def versionPropsFile = file('version.properties')
    if(versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        return versionProps['build'].toInteger()
    } else {
        throw new GradleException("Could no read properties")
    }
}

def setBuildVersion(buildVersion) {
    createPropertiesIfNotExist()
    def versionPropsFile = file('version.properties')
    if(versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionProps['build'] = buildVersion.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        throw new GradleException("Could no read properties")
    }
}

def incrementBuildVersion() {
    def buildOutputFolder = file('build/libs')
    buildOutputFolder.mkdirs()
    Arrays.asList(buildOutputFolder.listFiles()).forEach(file -> file.delete())

    def code = getBuildVersion() + 1
    setBuildVersion(code)
    version projectVersion + "-build" + code
}